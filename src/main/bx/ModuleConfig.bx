/**
 * This is the module descriptor and entry point for your module in the Runtime.
 * The unique name of the moduel is the name of the directory on the modules folder.
 * A BoxLang Mapping will be created for you with the name of the module.
 *
 * A Module can have the following folders that will be automatically registered:
 * + bifs - Custom BIFs that will be registered into the runtime
 * + interceptors - Custom Interceptors that will be registered into the runtime via the configure() method
 * + libs - Custom Java libraries that your module leverages
 * + tags - Custom tags that will be registered into the runtime
 *
 * Every Module will have it's own ClassLoader that will be used to load the module libs and dependencies.
 */
import ortus.boxlang.runtime.context.BaseBoxContext;
import ortus.boxlang.runtime.dynamic.casters.NumberCaster;

 class {

	// Injected at runtime by BoxLang
	property name="moduleRecord";
	property name="boxRuntime";
	property name="interceptorService";
	property name="functionService";
	property name="componentService";
	property name="cacheService";
	property name="asyncService";
	property name="schedulerService";
	property name="datasourceService";
	property name="log";

	// Static properties
	static {
		ADOBE = "adobe";
		LUCEE = "lucee";
	}

	/**
	 * --------------------------------------------------------------------------
	 * Module Properties
	 * --------------------------------------------------------------------------
	 * Here is where you define the properties of your module that the module service
	 * will use to register and activate your module
	 */

	/**
	 * Your module version. Try to use semantic versioning
	 * @mandatory
	 */
	this.version = "1.0.0";

	/**
	 * The BoxLang mapping for your module.  All BoxLang modules are registered with an internal
	 * mapping prefix of : bxModules.{this.mapping}, /bxmodules/{this.mapping}. Ex: bxModules.test, /bxmodules/test
	 */
	this.mapping = "compat-cfml";

	/**
	 * Who built the module
	 */
	this.author = "Luis Majano";

	/**
	 * The module description
	 */
	this.description = "The Adobe/Lucee compatibility module for BoxLang";

	/**
	 * The module web URL
	 */
	this.webURL = "https://www.ortussolutions.com";

	/**
	 * This boolean flag tells the module service to skip the module registration/activation process.
	 */
	this.enabled = true;

	/**
	 * --------------------------------------------------------------------------
	 * Module Methods
	 * --------------------------------------------------------------------------
	 */

	/**
	 * Called by the ModuleService on module registration
	 */
	function configure(){
		/**
		 * Every module has a settings configuration object
		 */
		settings = {
			// Choose your engine: adobe or lucee
			engine = "lucee",
			isLucee = false,
			isAdobe = false,
			clientManagement = false,
			clientStorage = "cookie",
			clientTimeout = createTimeSpan( 0, 1, 0, 0 ),
			// Mimic the CF behavior of nulls being undefined.  Set this to false to have full null support
			nullIsUndefined = true,
			// Mimic the CF and Lucee 5 behavior of boolean true/false being 1/0 and usable in math operations.  
			// Also affects the isNumeric() BIF.  To match Lucee 6, set this to false.
			booleansAreNumbers = true,
			// JSON control character auto-escaping flag
			// IF you turn to true, be aware that the entire JSON serialization will be escaped and be slower.
			jsonEscapeControlCharacters = true,
			// This simulates the query to empty value that Adobe/Lucee do when NOT in full null support
			// We default it to true to simulate Adobe/Lucee behavior
			queryNullToEmpty = true,
			// The CF -> BL AST transpiler settings
			// The transpiler is in the core, but will eventually live in this module, so the settings are here.
            transpiler = {
				// Turn foo.bar into foo.BAR
				upperCaseKeys = true,
				// Add output=true to functions and classes
				forceOutputTrue = true,
				// Merged doc comments into actual function, class, and property annotations
				mergeDocsIntoAnnotations = true
            }
		};

		/**
		 * Every module can have a list of object mappings
		 * that can be created by boxLang.  This is a great way
		 * to create objects that can be used by the module
		 * or other modules.
		 * The mappings will be created in the following format:
		 * bxModules.{this.mapping}.{mappingName}
		 * Ex: bxModules.test.MyObject => bxModules.test.models.MyObject
		 */
		objectMappings = {
			// { name="MyObject", class="models.utilities.MyObject" }
		}

		/**
		 * The module interceptors to register into the runtime
		 */
		interceptors = [
			// { class="path.to.Interceptor", properties={} }
		];

		/**
		 * A list of custom interception points to register into the runtime
		 */
		customInterceptionPoints = [
			"onClientCreated",
			"onClientDestroyed"
		];
	}

	/**
	 * Called by the ModuleService on module activation
	 */
	function onLoad(){
		// Set the nullIsUndefined flag.  This is stored statically on the base context for best performance
		BaseBoxContext.nullIsUndefined = settings.nullIsUndefined;

		// Set this statically for best performance
		NumberCaster.booleansAreNumbers = settings.booleansAreNumbers;

		// Verify Engine
		if( isNull( settings.engine ) || settings.engine.isEmpty() ){
			throw( "You must specify an [engine] in the settings. Valid engines are [adobe,lucee]" );
		}

		// Use the boolean identifier
		if( settings.engine == "lucee" ){
			settings.isLucee = true;
		} else if( settings.engine == "adobe" ){
			settings.isAdobe = true;
		}

		// Load the Lucee or Adobe compatibility interceptor
		if( settings.isLucee ){
			// Register Lucee compatibility with the interceptor service
			interceptorService.newAndRegister(
				// Class path
				"#moduleRecord.invocationPath#.interceptors.LuceeServerScope",
				// Properties Struct
				settings,
				// Unique Name
				"LuceeServerScope@compat",
				// Module Record
				moduleRecord
			);
		}

		if( settings.isAdobe ){
			// Register Adobe compatibility with the interceptor service
			interceptorService.newAndRegister(
				// Class path
				"#moduleRecord.invocationPath#.interceptors.AdobeServerScope",
				// Properties Struct
				settings,
				// Unique Name
				"AdobeServerScope@compat",
				// Module Record
				moduleRecord
			);
		}
		// Register SQLType compatibility interceptor
		interceptorService.newAndRegister(
			// Class path
			"#moduleRecord.invocationPath#.interceptors.QueryCompat",
			// Properties Struct
			settings,
			// Unique Name
			"QueryCompat@compat",
			// Module Record
			moduleRecord
		);

		// Register the Client Scope Cache
		var cacheService = boxRuntime.getCacheService();
		if ( !cacheService.hasCache( "bxClients" ) ) {
			boxRuntime.getCacheService().createCache(
				"bxClients",
				"BoxCacheProvider",
				{
					"evictCount": 1,
					"evictionPolicy": "LRU",
					"freeMemoryPercentageThreshold": 0,
					"maxObjects": 100000,
					"defaultLastAccessTimeout": 3600,
					"defaultTimeout": 3600,
					"objectStore": "ConcurrentStore",
					"reapFrequency": 120,
					"resetTimeoutOnAccess": true,
					"useLastAccessTimeouts": false
				}
			);
		}

		// Register the CFIDE Mapping for compatibility
		boxRuntime.getConfiguration().registerMapping( "CFIDE", variables.moduleRecord.path & "/CFIDE" );
	}

	/**
	 * Called by the ModuleService on module deactivation
	 */
	function onUnload(){

	}

	/**
	 * --------------------------------------------------------------------------
	 * Module Events
	 * --------------------------------------------------------------------------
	 * You can listen to any Runtime events by creating the methods
	 * that match the approved Runtime Interception Points
	 */

}
